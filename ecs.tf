data "aws_region" "current" {}

locals {
  // AWS_REGION is needed to have your aws default config correctly manage the region.
  task_env_variables = concat(var.environment_variables, [
    { name : "ENV", value : var.environment },
    // { name : "AWS_REGION", value : data.aws_region.current },
  ])
}

resource "aws_ecs_task_definition" "aws-ecs-task" {
  family = "${var.service_name}-task"

  runtime_platform {
    operating_system_family = "LINUX"
    cpu_architecture        = "ARM64"
  }

  container_definitions = <<DEFINITION
  [
    {
      "name": "main",
      "image": "${var.image}",
      "entryPoint": [],
      "environment": ${jsonencode(local.task_env_variables)},
      "essential": true,
      "logConfiguration": {
        "logDriver": "awslogs",
        "options": {
          "awslogs-group": "${aws_cloudwatch_log_group.log-group.id}",
          "awslogs-region": "us-east-1",
          "awslogs-stream-prefix": "${var.service_name}-${var.environment}"
        }
      },
      "portMappings": [
        {
          "containerPort": ${var.container_port},
          "hostPort": ${var.container_port},
          "protocol": "tcp"
        }
      ],
      "cpu": ${var.cpu},
      "memory": ${var.memory},
      "networkMode": "awsvpc"
    }
  ]
  DEFINITION

  requires_compatibilities = [var.use_ec2 ? "EC2" : "FARGATE"]
  network_mode             = "awsvpc"
  memory                   = var.memory
  cpu                      = var.cpu
  execution_role_arn       = aws_iam_role.service.arn
  task_role_arn            = aws_iam_role.service.arn

  tags = {
    Name = "${var.service_name}-ecs-td"
  }
}

data "aws_ecs_task_definition" "main" {
  task_definition = aws_ecs_task_definition.aws-ecs-task.family
}

resource "aws_ecs_service" "aws-ecs-service" {
  name                   = var.service_name
  cluster                = var.ecs_cluster_arn
  task_definition        = "${aws_ecs_task_definition.aws-ecs-task.family}:${max(aws_ecs_task_definition.aws-ecs-task.revision, data.aws_ecs_task_definition.main.revision)}"
  desired_count          = var.desired_count
  force_new_deployment   = true
  enable_execute_command = true
  propagate_tags         = "SERVICE"

  dynamic "capacity_provider_strategy" {
    for_each = var.capacity_providers
    content {
      capacity_provider = capacity_provider_strategy.value.capacity_provider
      base              = capacity_provider_strategy.value.base
      weight            = capacity_provider_strategy.value.weight
    }
  }

  ordered_placement_strategy {
    type  = "spread"
    field = "attribute:ecs.availability-zone"
  }

  network_configuration {
    subnets          = var.subnets
    assign_public_ip = false
    security_groups  = concat([aws_security_group.service.id, var.lb_security_group_id], var.security_groups)
  }

  load_balancer {
    target_group_arn = aws_lb_target_group.service.arn
    container_name   = "main"
    container_port   = var.container_port
  }

  lifecycle {
    ignore_changes = [desired_count]
  }
}
